S -> SP_LIST P
SP_LIST -> epsilon
SP_LIST -> SP SP_LIST
SP -> subproceso id COMPLEMENT OPT_SP C finsubproceso
COMPLEMENT -> token_asig id
COMPLEMENT -> epsilon
OPT_SP -> epsilon
OPT_SP -> token_par_izq OPT_PARAM token_par_der
OPT_PARAM -> id ID_LIST
OPT_PARAM -> epsilon
OPT_ARGS -> token_par_izq ARGS token_par_der
ARGS -> epsilon
ARGS -> E E_LIST
P -> MAIN_BEGIN id C MAIN_END
MAIN_BEGIN -> proceso
MAIN_BEGIN -> algoritmo
MAIN_END -> finproceso
MAIN_END -> finalgoritmo
C -> D token_pyc C
C -> epsilon
C -> id C_OPT token_pyc C
C -> READ token_pyc C
C -> WRITE token_pyc C
C -> DIM token_pyc C
C -> IF C
C -> FOR C
C -> WHILE C
C -> DOWHILE C
C -> SWITCH C
C -> WAIT token_pyc C
C -> CLEAR token_pyc C
C_OPT -> token_asig E
C_OPT -> OPT_ARGS
IF -> si E entonces C END_IF
END_IF -> finsi
END_IF -> sino C finsi
FOR -> para id token_asig E hasta EXP_NUM OPT_FOR hacer C finpara
OPT_FOR -> con paso EXP_NUM
OPT_FOR -> epsilon
WHILE -> mientras E hacer C finmientras
DOWHILE -> repetir C hasta que E
SWITCH -> segun EXP_NUM hacer OPT_CASE OPT_DEFAULT finsegun
OPT_CASE -> caso EXP_NUM token_dosp C OPT_CASE
OPT_CASE -> epsilon
OPT_DEFAULT -> epsilon
OPT_DEFAULT -> de otro modo token_dosp C
WAIT -> esperar WAIT_TYPE
WAIT_TYPE -> tecla
WAIT_TYPE -> EXP_NUM UNITY
UNITY -> segundos
UNITY -> milisegundos
CLEAR -> borrar SCREEN
CLEAR -> limpiar SCREEN
SCREEN -> pantalla
DIM -> dimension DEF_DIM
DEF_DIM -> id token_cor_izq EXP_NUM EXP_NUM_LIST token_cor_der DEF_DIM_LIST
DEF_DIM_LIST -> epsilon
DEF_DIM_LIST -> token_coma DEF_DIM DEF_DIM_LIST
EXP_NUM_LIST -> epsilon
EXP_NUM_LIST -> token_coma EXP_NUM EXP_NUM_LIST
READ -> leer id ID_LIST
WRITE -> escribir E E_LIST
E_LIST -> token_coma E E_LIST
E_LIST -> epsilon
D -> definir id ID_LIST como TIPO
ID_LIST -> token_coma id ID_LIST
ID_LIST -> epsilon
TIPO -> entero
TIPO -> real
TIPO -> numero
TIPO -> numerico
TIPO -> texto
TIPO -> cadena
TIPO -> caracter
TIPO -> logico
E -> B_TERM OPT_OR
OPT_OR -> epsilon
OPT_OR -> token_o B_TERM OPT_OR
B_TERM -> NOT_FACTOR OPT_AND
OPT_AND -> epsilon
OPT_AND -> token_y NOT_FACTOR OPT_AND
NOT_FACTOR -> token_neg B_FACTOR
NOT_FACTOR -> B_FACTOR
B_FACTOR -> verdadero
B_FACTOR -> falso
B_FACTOR -> EXP_NUM EXP_NUM_COM
EXP_NUM_COM -> epsilon
EXP_NUM_COM -> REL_OPT EXP_NUM EXP_NUM_COM
REL_OPT -> token_igual
REL_OPT -> token_dif
REL_OPT -> token_mayor
REL_OPT -> token_mayor_igual
REL_OPT -> token_menor
REL_OPT -> token_menor_igual
EXP_NUM -> TERM OPT_MOD
OPT_MOD -> token_mod TERM OPT_MOD
OPT_MOD -> epsilon
TERM -> MOD_TERM OPT_SUM
OPT_SUM -> epsilon
OPT_SUM -> SUM_OPT MOD_TERM OPT_SUM
MOD_TERM -> S_TERM OPT_MUL
OPT_MUL -> epsilon
OPT_MUL -> MUL_OPT S_TERM OPT_MUL
MUL_OPT -> token_mul
MUL_OPT -> token_div
S_TERM -> M_TERM OPT_POT
OPT_POT -> token_pot M_TERM OPT_POT
OPT_POT -> epsilon
M_TERM -> FACTOR
SUM_OPT -> token_mas
SUM_OPT -> token_menos
FACTOR -> token_cadena
FACTOR -> token_real
FACTOR -> token_entero
FACTOR -> id OPT_FUNCTION
OPT_FUNCTION -> OPT_ARGS
OPT_FUNCTION -> epsilon
FACTOR -> token_par_izq E token_par_der